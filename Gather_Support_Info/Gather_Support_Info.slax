/*
$Id$
*/

version 1.0;
ns Junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns exsl extension = "http://exslt.org/common";

import "../import/junos.xsl";

/*
 * Author        : Nadeem Mohammad
 * Version       : 1.0
 * Last Modified : April 7th, 2014
 * Platform      : MX
 * Release       : 10.4 and above
 *
 * Description   : This op script takes case number as an argument and then
 *                 1. Gathers system support info in /var/log/case-number.log file
 *                 2. compress contents of /var/log/ and put it in /var/tmp/case-number.tgz
 *                 3. Anonymous ftp the case-number.tgz to ftp.juniper.net and put it in
 *                    /put/incoming/ directory. 
 *
 * =============================================================================
 * Revision History
 * -----------------------------------------------------------------------------
 * 1.0: April 7th, 2014
 *      - Initial Release
 * 
 *
 * =============================================================================
 *
 * To set this up on your JUNOS device copy this script to the following folder:
 * /var/db/scripts/op
 *
 * Make sure you name the script 'IT-script' or change references in
 * the script and JUNOS config to be whatever you named it.
 *
 */

var $arguments = {
        <argument> {
                <name> "case-number";
                <description> "jtac case number";
        }
}

param $case-number = 0;

match / {

  <op-script-results> {

  if ($case-number != 0 ) {

     var $support-file-name = $case-number _ ".log";

     /* get the support information and save it to /var/log */
     var $cmd = <command format="text"> "request support information";
     var $result = jcs:invoke($cmd);
     <xsl:document method="text" href="/var/log/" _ $support-file-name> {
          copy-of $result;
     }



     /* Archive contents of /var/log dir */
     var $file-archive-rpc = {
        <file-archive> {
            <compress>;
            <source> "/var/log";
            <destination> "/var/tmp/" _ $case-number _ ".tgz";
        }
     }

     var $archive-results = jcs:invoke( $file-archive-rpc );

     /* ftp the contents to the ftp server ftp.juniper.net */
     var $directory = "/var/tmp/";
     var $file-copy-rpc = {
         <file-copy> {
             <source> $directory _ $case-number _ ".tgz";
             <destination> "ftp://anonymous:@ftp.juniper.net///pub/incoming/";
         }
     }

     var $copy-results = jcs:invoke( $file-copy-rpc );

     if( $copy-results/..//xnm:error ) {
         for-each( $copy-results/..//xnm:error ) {
            expr jcs:output( message );
         }
         <xsl:message terminate="yes"> "Error copying script to " _ .;
     }

     expr jcs:output("[Success]");
  }
  }
}